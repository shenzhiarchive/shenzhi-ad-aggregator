buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['AdAggregator_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["AdAggregator_" + name]).toInteger()
}

android {
  namespace "com.shenzhi.adaggregator"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    debug {
      // Debug构建类型配置
      debuggable true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
  flatDir {
    //本地aar文件目录，使用相对路径（相对于build.gradle文件所在目录）
    dirs 'libs', './libs/adapter', './libs/adn'
  }
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  
  //穿山甲SDK必需依赖（参考官方文档方式二）
  implementation 'com.squareup.okhttp3:okhttp:3.12.1'
  
  //导入libs目录下的所有jar文件
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  
  //导入libs目录下的aar文件（不包括测试工具）
  implementation fileTree(include: ['*.aar'], dir: 'libs', exclude: ['tools-release.aar'])
  
  //导入adapter和adn目录下的所有aar文件
  implementation fileTree(include: ['*.aar'], dir: 'libs/adapter')
  implementation fileTree(include: ['*.aar'], dir: 'libs/adn')
  
  //穿山甲测试工具（仅在Debug构建中包含，Release构建自动排除）
  debugImplementation fileTree(include: ['tools-release.aar'], dir: 'libs')
  
  //---------- AdMob ----------
  implementation("com.google.android.gms:play-services-ads:17.2.0") {
    exclude group: 'com.android.support'
  }
}

